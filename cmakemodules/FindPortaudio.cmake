# - Try to find Portaudio
# Once done this will define
#  PORTAUDIO_FOUND        - System has portaudio
#  PORTAUDIO_INCLUDE_DIRS - The portaudio include directories
#  PORTAUDIO_LIBRARIES    - The libraries needed to use portaudio
#  PORTAUDIO_DEFINITIONS  - Compiler switches required for using portaudio

if (PORTAUDIO_LIBRARIES AND PORTAUDIO_INCLUDE_DIRS)

	# in cache already
	set(PORTAUDIO_FOUND TRUE)

else (PORTAUDIO_LIBRARIES AND PORTAUDIO_INCLUDE_DIRS)

	set(PORTAUDIO_DEFINITIONS "")

	# Look for pkg-config and use it (if available) to find package
	find_package(PkgConfig QUIET)
	if (PKG_CONFIG_FOUND)
		pkg_search_module(PORTAUDIO QUIET portaudio-2.0)
	endif (PKG_CONFIG_FOUND)

	if (NOT PORTAUDIO_FOUND)

		find_path(PORTAUDIO_INCLUDE_DIR portaudio.h HINTS ${PORTAUDIO_INCLUDEDIR} ${PORTAUDIO_INCLUDE_DIRS} PATH_SUFFIXES portaudio)

		if (WIN32 AND MSVC)
			# If we are building with Visual Studio, we assume here that
			# PortAudio was also built with Visual Studio using the CMake
			# project files. The DLLs generated by these end up with platform
			# specific suffixes.
			if ("${CMAKE_VS_PLATFORM_NAME}" STREQUAL "x64")
				find_library(PORTAUDIO_LIBRARY NAMES portaudio_x64 HINTS ${PORTAUDIO_LIBDIR} ${PORTAUDIO_LIBRARY_DIRS})
			else ("${CMAKE_VS_PLATFORM_NAME}" STREQUAL "x64")
				find_library(PORTAUDIO_LIBRARY NAMES portaudio_x86 HINTS ${PORTAUDIO_LIBDIR} ${PORTAUDIO_LIBRARY_DIRS})
			endif ("${CMAKE_VS_PLATFORM_NAME}" STREQUAL "x64")
		else (WIN32 AND MSVC)
			find_library(PORTAUDIO_LIBRARY NAMES portaudio HINTS ${PORTAUDIO_LIBDIR} ${PORTAUDIO_LIBRARY_DIRS})
		endif (WIN32 AND MSVC)

		set(PORTAUDIO_LIBRARIES    ${PORTAUDIO_LIBRARY})
		set(PORTAUDIO_INCLUDE_DIRS ${PORTAUDIO_INCLUDE_DIR})

		include(FindPackageHandleStandardArgs)

		# Set PORTAUDIO_FOUND if the library and include paths were found
		find_package_handle_standard_args(portaudio DEFAULT_MSG PORTAUDIO_LIBRARY PORTAUDIO_INCLUDE_DIR)

		# Don't show include/library paths in cmake GUI
		mark_as_advanced(PORTAUDIO_INCLUDE_DIR PORTAUDIO_LIBRARY)

	endif (NOT PORTAUDIO_FOUND)

endif (PORTAUDIO_LIBRARIES AND PORTAUDIO_INCLUDE_DIRS)
